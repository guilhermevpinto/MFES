class Game

instance variables
	private players : seq of Player := [];
	private currentPlayerID : nat1 := 1;
	private board : [Board];

operations
	
		public Game : nat1 ==> Game
		Game(number) ==
		(
			if number = 2
			then players := [new Player(1), new Player(2)]
			else if number = 4 
			then players := [new Player(1), new Player(2), new Player(3), new Player(4)];
			currentPlayerID := 1;
			board := new Board(self);
			updateBoard();
		);
		
		-- sets a new position in the board for a given player
		public move: nat1 * nat1 * [Player] ==> ()
		move(new_x, new_y, player) ==
		(
			eraseOldPosition(player.getPosition());
			player.setPosition(new_x, new_y);
			updateBoard();
		)
		pre player <> nil
		post player.getPosition().getX() = new_x and player.getPosition().getY() = new_y;

		-- switches to another player
		public switchPlayer : () ==> ()
		switchPlayer() == 
		if len players = 2
		then
		(
			if currentPlayerID = 1 then currentPlayerID := 2 
			else currentPlayerID := 1;
		)
		else if len players = 4
		then
		(
			if currentPlayerID = 1 then currentPlayerID := 2 
			else if currentPlayerID = 2 then currentPlayerID := 3
			else if currentPlayerID = 3 then currentPlayerID := 4 
			else currentPlayerID := 1;
		);
		
		public getPlayer :nat1 ==>Player 
		getPlayer(id) ==
		return [player | player in seq players  &  player.getPlayerID() = id](1);
		
		public getCurrentPlayer : () ==> nat1
		getCurrentPlayer() ==
		return currentPlayerID;
		
		-- verify wether the player won
		public currentPlayerWin : () ==> bool
		currentPlayerWin() ==
		(
			if (getPlayer(currentPlayerID).getTargetRow() <> nil and getPlayer(currentPlayerID).getPosition().getX() = getPlayer(currentPlayerID).getTargetRow()) 
					or 
				(getPlayer(currentPlayerID).getTargetCol() <> nil and getPlayer(currentPlayerID).getPosition().getY() = getPlayer(currentPlayerID).getTargetCol())
					then return true
			else return false;		
		);
		
		public addPlayer: Player ==> ()
			addPlayer(p) ==
			(
				players := players^[p];
			)
			post len players <> 0;

		-- retrieve board from game instance
		public getBoard : () ==> Board
		getBoard() == return self.board;
		
		-- retrieve board from game instance
		public getPlayers : () ==> seq of Player
		getPlayers() == return self.players;
		
		public addWall : nat1 * nat1 ==> bool
		addWall(row, col) == board.addWall(row,col,players);
		
		public getPossibleMoves: () ==> seq of Point
		getPossibleMoves() ==
		(
			dcl moves: seq of Point := [],
			special_moves: seq of Point := [],
			p: Player := getPlayer(currentPlayerID),
			p_x: nat1 := p.getPosition().getX(),
			p_y: nat1 := p.getPosition().getY(),
			x: nat1, y: nat1;
			
			-- upper position exists 
			if(p_x <> 1)
			then
			(
				if board.board(p_x-1)(p_y) = <NOWALL>
				then
				(
					if board.board(p_x-2)(p_y) = <FREE>
					then
					(
						x := (p_x-2);
						y := p_y;
						moves := moves^[new Point(x, y)];
					)
					else
					(
						-- jump over another player
						special_moves := special_moves^verifyPlayerJump(p_x-2, p_y, "up");
						moves := moves^special_moves;
					);
				);
			);
			-- left position exists
			if(p_y <> 1)
			then
			(
				if board.board(p_x)(p_y-1) = <NOWALL>
				then
				(
					if board.board(p_x)(p_y-2) = <FREE>
					then
					(
						x := p_x;
						y := (p_y-2);
						moves := moves^[new Point(x, y)];
					)
					else
					(
						-- jump over another player
						special_moves := special_moves^verifyPlayerJump(p_x, p_y-2, "left");
						moves := moves^special_moves;
					);
				);
			);
			-- right position exists
			if(p_y <> 17)
			then
			(
				if board.board(p_x)(p_y+1) = <NOWALL>
				then
				(
					if board.board(p_x)(p_y+2) = <FREE>
					then
					(
						x := p_x;
						y := (p_y+2);
						moves := moves^[new Point(x, y)];
					)
					else
					(
						-- jump over another player
						special_moves := special_moves^verifyPlayerJump(p_x, p_y+2, "right");
						moves := moves^special_moves;
					);
				);
			);
			-- bottom position exists
			if(p_x <> 17)
			then
			(
				if board.board(p_x+1)(p_y) = <NOWALL>
				then
				(
					if board.board(p_x+2)(p_y) = <FREE>
					then
					(
						x := (p_x+2);
						y := p_y;
						moves := moves^[new Point(x, y)];
					)
					else
					(
						-- jump over another player
						special_moves := special_moves^verifyPlayerJump(p_x+2, p_y, "down");
						moves := moves^special_moves;
					);
				);
			);
			
			return moves;
		);
		
		-- verifies whether the player can jump over another player
		public verifyPlayerJump: nat1 * nat1 * seq of char ==> seq of Point
		verifyPlayerJump(x, y, direction) ==
		(
			if board.board(x)(y) = <OCCUPIED>
			then
			(
				if direction = "up"
				then
				(
					return checkUpMove(x, y);
				)
				else
				(
					if direction = "left"
					then
					(
						return checkLeftMove(x, y);
					)
					else
					(
						if direction = "right"
						then
						(
							return checkRightMove(x, y);
						)
						else
						(
							if direction = "down"
							then
							(
								return checkDownMove(x, y);
							)
							else
							(
								return [];
							);
						);
					);
				);
			)
			else
			(
				return [];
			);
		);
		
		-- check possible up movement
		private checkUpMove : nat1 * nat1 ==> seq of Point
		checkUpMove(x, y) ==
		(
		
			dcl special: seq of Point := [];
			
			if(x <> 1)
			then
			(
				if board.board(x-1)(y) = <NOWALL>
				then
				(
					if board.board(x-2)(y) = <FREE>
					then
					(
						special := special^[new Point(x-2, y)];
					)
				)
				-- check special case with oponent + wall followed by each other
				else
				(
					special := special^checkDiagonalHorizontal(x, y);
				);
			);
			
			return special;
				
		);
		
		-- check possible left movement
		private checkLeftMove : nat1 * nat1 ==> seq of Point
		checkLeftMove(x, y) ==
		(
			
			dcl special: seq of Point := [];
			
			if(y <> 1)
			then
			(
				if board.board(x)(y-1) = <NOWALL>
				then
				(
					if board.board(x)(y-2) = <FREE>
					then
					(
						special := special^[new Point(x, y-2)];
					)
				)
				-- check special case with oponent + wall followed by each other
				else
				(
					special := special^checkDiagonalVertical(x, y);
				);
			);
			
			return special;
			
		);
		
		-- check possible right movement
		private checkRightMove : nat1 * nat1 ==> seq of Point
		checkRightMove(x, y) ==
		(	
		
			dcl special: seq of Point := [];
			
			if(y <> 17)
			then
			(
				if board.board(x)(y+1) = <NOWALL>
				then
				(
					if board.board(x)(y+2) = <FREE>
					then
					(
						special := special^[new Point(x, y+2)];
					)
				)
				-- check special case with oponent + wall followed by each other
				else
				(
					special := special^checkDiagonalVertical(x, y);
				);
			);
			
			return special;
			
		);
		
		-- check possible down movement
		private checkDownMove : nat1 * nat1 ==> seq of Point
		checkDownMove(x, y) ==
		(
		
			dcl special: seq of Point := [];
		
			if(x <> 17)
			then
			(
				if board.board(x+1)(y) = <NOWALL>
				then
				(
					if board.board(x+2)(y) = <FREE>
					then
					(
						special := special^[new Point(x+2, y)];
					)
				)
				-- check special case with oponent + wall followed by each other
				else
				(
					special := special^checkDiagonalHorizontal(x, y);
				);
			);
			
			return special;
			
		);
		
		-- check diagonal movement direction horizontal in special cases
		private checkDiagonalHorizontal : nat1 * nat1 ==> seq of Point
		checkDiagonalHorizontal(x, y) ==
		(
		
			dcl special: seq of Point := [];
			
			-- check if the movement to the left is possible
			if y <> 1
			then
			(
				-- check for walls on the left
				if board.board(x)(y-1) = <NOWALL>
				then
				(
					if board.board(x)(y-2) = <FREE>
					then
					(
						special := special^[new Point(x, y-2)];
					)
				);
			);
			-- check if the movement to the right is possible
			if y <> 17
			then
			(
				-- check for walls on the right
				if board.board(x)(y+1) = <NOWALL>
				then
				(
					if board.board(x)(y+2) = <FREE>
					then
					(
						special := special^[new Point(x, y+2)];
					)
				);
			);
			
			return special;
			
		);
		
		-- check diagonal movement direction vertical in special cases
		private checkDiagonalVertical : nat1 * nat1 ==> seq of Point
		checkDiagonalVertical(x, y) ==
		(
		
			dcl special: seq of Point := [];
			
			-- check if the movement up is possible
			if x <> 1
			then
			(
				-- check for walls up
				if board.board(x-1)(y) = <NOWALL>
				then
				(
					if board.board(x-2)(y) = <FREE>
					then
					(
						special := special^[new Point(x-2, y)];
					)
				);
			);
			-- check if the movement down is possible
			if x <> 17
			then
			(
				-- check for walls down
				if board.board(x+1)(y) = <NOWALL>
				then
				(
					if board.board(x+2)(y) = <FREE>
					then
					(
						special := special^[new Point(x+2, y)];
					)
				);
			);
		
			return special;
			
		);
		
		-- update board according to players positions
		public updateBoard : () ==> () 
		updateBoard() ==
		(
			dcl player1: Player := getPlayer(1),
			player2: Player := getPlayer(2),
			p1_position: Point := player1.getPosition(),
			p2_position: Point := player2.getPosition();
			
			board.setBoardPosition(p1_position);
			board.setBoardPosition(p2_position);
		);
		
		-- update board by erasing the old position
		public eraseOldPosition(old_position: Point) ==
		(
			board.unsetBoardPosition(old_position);
		);

end Game